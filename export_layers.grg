// Export each layer to PNG with alpha

// GETTING FILENAME AND DEFINING PREFIX
tv_getprojectname
filePath = result

position = LastPos(filePath,'\')

filePathLength = LEN(filePath)
fileName = filePathWithoutExtension = SubString(filePath,position+1,filePathLength-5)


fileNameIntegrity = CheckFileName(fileName,'-')
IF (fileNameIntegrity != 5)
  tv_Warn 'Check that the file matches this pattern:\n[PROJECT]-SC[SHOT NUMBER]-[CONTENT: anim/char/comp/etc..)-[YOUR NAME]-V[VERSION NUMBER]\nfor example: Sand-SC003-color-dorian-v13'
  EXIT
END

fileVar = Replace(fileName,'-',' ')
PARSE fileVar projectName shot type artist shotVersion

prefix = projectName'-'shot'-'shotVersion


Param none

Extension = '.png'
tv_SaveMode 'png' 'b32' 'nodither'
tv_alphaloadmode 'nopremultiply'
tv_alphasavemode 'nopremultiply'


tv_LayerCurrentID
OriginLayer = result

tv_ReqString 'Export layers to...'
path = result
path = concat(path,'\')


tv_background 'none' // setting background to black

layerIdx = 0
loopEnded = 0
markIn = 99999

DO
  tv_LayerGetID layerIdx
  layerID = result

  if (CMP(layerID,'None')==1)
    loopEnded = 1
  else
    tv_LayerInfo layerID
    parse result display position opacity name layerType layerStart layerEnd misc
    layerID[layerIdx] = layerID
    tv_LayerDisplay layerID OFF     // turns off the layer display
    state[layerIdx] = result
    name[layerIdx] = name
    layerEnd[layerIdx] = layerEnd
    layerStart[layerIdx] = layerStart
    if (layerStart < markIn)
      markIn = layerStart
    end
    if (CMP(display,'ON')==1)
      exportLayer[layerIdx] = 'export'
    else
      exportLayer[layerIdx] = 'do not export'
    end
  end
  layerIdx = layerIdx+1
UNTIL loopEnded

nbOfLayers = layerIdx-2

layerOrderNumber = 0

FOR layerIdx = 0 TO nbOfLayers
  if (CMP(exportLayer[layerIdx], 'export')==1)
    tv_LayerDisplay layerID[layerIdx] 'ON'

    layerZHeight = layerOrderNumber
    layerOrderNumber = layerOrderNumber + 1

    layername = name[layerIdx]
    filename = prefix'-'layername'-'layerOrderNumber'-''0000'Extension
    savepath = path''filename
    layerMarkOut = layerEnd[layerIdx]

    tv_saveSequence savepath markIn layerMarkOut
    tv_LayerDisplay layerID[layerIdx] 'OFF'
  end
END

FOR layerIdx = 0 TO nbOfLayers
 tv_LayerDisplay layerID[layerIdx] state[layerIdx]
END

tv_LayerSet OriginLayer
tv_background 'color'

EXIT

#INCLUDE "advanced.grg"

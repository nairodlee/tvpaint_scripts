// Export each layer to PNG with alpha

Param none

tv_Request 'This script will only export displayed layers to the designated location.\nThe naming will be like so: [PREFIX]_[LAYER_NAME]_[SUFFIX]_0000.\nPlease give a unique name to each layer without space|Continue|Cancel'
input = result
if (input == 0)
  EXIT
end

Extension = '.png'
tv_SaveMode 'png' 'b32' 'nodither'
tv_alphaloadmode 'nopremultiply'
tv_alphasavemode 'nopremultiply'

tv_LayerCurrentID
OriginLayer = result

tv_ReqString 'Export layers to...'
path = result
path = concat(path,'\')


tv_ReqString 'Enter file name prefix (can leave blank)'
prefix = result
if (CMP(prefix, 0)==1)
  prefix = ''
else
  prefix = prefix'_'
end

tv_ReqString 'Enter file name suffix (can leave blank)'
suffix = result
if (CMP(suffix, 0)==1)
  suffix = ''
else
  suffix = '_'suffix'_'
end

// returns null
// can't get clipLastFrame to set as max value for markIn and markOut
// tv_clipInfo
// PARSE result clipName IsCurrent IsHidden IsSelected StoryboardStartFrame FirstFrame clipLastFrame misc


tv_layergetimage
currentFrame = result

// tv_ReqNum [initial] [min] [max] [window title]
tv_ReqNum currentFrame 0 10000 'Start exporting from frame number...'
markIn = result
if (CMP(markIn, 'Cancel')==1)
  markIn = 0
end

tv_ReqNum 0 0 10000 'Export until frame number... (0 or Cancel for auto)'
markOut = result
if (CMP(markOut, 'Cancel')==1)
  markOut = 0
end

tv_background 'none'


layerIdx = 0
loopEnded = 0

DO
  tv_LayerGetID layerIdx
  layerID = result

  if (CMP(layerID,'None')==1)
    loopEnded = 1
  else
    tv_LayerInfo layerID
    parse result display position opacity name layerType layerStart layerEnd misc
    layerID[layerIdx] = layerID
    tv_LayerDisplay layerID OFF     // turns off the layer display
    state[layerIdx] = result
    name[layerIdx] = name
    layerEnd[layerIdx] = layerEnd

    if (CMP(display,'ON')==1)
      exportLayer[layerIdx] = 'export'
    else
      exportLayer[layerIdx] = 'do not export'
    end
  end
  layerIdx = layerIdx+1
UNTIL loopEnded

nbOfLayers = layerIdx-2

FOR layerIdx = 0 TO nbOfLayers
  if (CMP(exportLayer[layerIdx], 'export')==1)
    tv_LayerDisplay layerID[layerIdx] 'ON'

    layername = name[layerIdx]
    filename = prefix''layername''suffix'0000'Extension
    savepath = path''filename
    layerMarkOut = markOut

    if (layerMarkOut == 0)
      layerMarkOut = layerEnd[layerIdx]
    end

    tv_saveSequence savepath markIn layerMarkOut
    tv_LayerDisplay layerID[layerIdx] 'OFF'
  end
END

FOR layerIdx = 0 TO nbOfLayers
 tv_LayerDisplay layerID[layerIdx] state[layerIdx]
END

tv_LayerSet OriginLayer
tv_background 'color'

EXIT
